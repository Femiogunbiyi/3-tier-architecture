# Application Load Balancer
resource "aws_lb" "main" {
  name               = "${var.environment}-${var.project_name}-${var.name_prefix}alb"
  internal           = var.internal
  load_balancer_type = "application"
  security_groups    = [var.security_group_id]
  subnets            = var.subnets_ids

  enable_deletion_protection = var.enable_deletion_protection
  enable_cross_zone_load_balancing = true
  enable_http2 = true
  idle_timeout = 60

   tags = merge(
    var.tags,
    {
       Name =  "${var.environment}-${var.project_name}-alb"
    }
  )
}

# Target Group
resource "aws_lb_target_group" "main" {
  name        = "${var.environment}-${var.project_name}-${var.name_prefix}tg"
  port        = var.target_group_port
  protocol    = "HTTP"
  vpc_id      = var.vpc_id

  health_check {
    enabled = true
    healthy_threshold = 2
    interval = 30
    matcher = "200-299"
    path = "/health"
    port = "traffic-port"
    protocol = "HTTP"
    timeout = 6
    unhealthy_threshold = 3
  }

  deregistration_delay = 30

  stickiness {
    type = "lb_cookie"
    cookie_duration = 86400 
    enabled = true
  }

  tags = merge(
    var.tags,
    {
       Name =  "${var.environment}-${var.project_name}-frontend-tg"
    }
  )
}

# HTTP Listener
resource "aws_lb_listener" "http" {
  load_balancer_arn = aws_lb.main.arn
  port              = "80"
  protocol          = "HTTP"

  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.main.arn
  }

  tags = var.tags
}

# HTTPS Listener (optional, requires certificate)
resource "aws_lb_listener" "front_end" {
  count = var.certificate_arn != "" ? 1 : 0
  load_balancer_arn = aws_lb.main.arn
  port              = "443"
  protocol          = "HTTPS"
  ssl_policy        = "ELBSecurityPolicy-2016-08"
  certificate_arn   = var.certificate_arn

  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.main.arn
  }

  tags = var.tags
}

# HTTP to HTTPS redirect (optional)
resource "aws_lb_listener_rule" "redirect_http_to_https" {
  count = var.certificate_arn != "" ? 1 : 0
  listener_arn = aws_lb_listener.http.arn

  action {
    type = "redirect"

    redirect {
      port        = "443"
      protocol    = "HTTPS"
      status_code = "HTTP_301"
    }
  }

  condition {
    path_pattern {
      values = ["/*"]
    }
}
}
